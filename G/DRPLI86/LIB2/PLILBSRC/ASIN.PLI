 asin: proc(arg) returns(float bin(24));		
 dcl arg                 float bin(24);
 dcl parm_table(0:9)     float bin(24) static init
 (  1.4866665,
     .038853035,
     .00288544125,
     .000288421825,
     .00003322367,
     .0000041584777,
     .00000054965045,
     .00000007550078,
     .000000010671938,
     .0000000015421804);
 dcl float_m1            float bin(24) static init(-1);
 dcl float_1             float bin(24) static init(1);
 dcl float_0             float bin(24) static init(0);
 dcl float_mv7071068     float bin(24) static init(-.7071068);
 dcl float_m1v57079625   float bin(24) static init(-1.57079625);
 dcl float_v7071068      float bin(24) static init(.7071068);
 dcl float_1v57079625    float bin(24) static init(1.57079625);
 dcl float_rt2           float bin(24) static init(1.4142135);
 dcl fixed_2             fixed bin(15) static init(2);
 dcl float_2             float bin(24) static init(2);
 dcl table_limit         fixed bin(15) static init(9);
 dcl byte                fixed bin(7)  based;
 dcl hold_arg            float bin(24);
 dcl (temp1, temp2)      float bin(24);
 dcl i                   fixed bin(7);
 dcl ?fpex2           entry(fixed bin(7), float bin(24)) returns(float bin(24));
 dcl ?cheby           entry(float bin(24), fixed bin(7), (0:9) float bin(24))
                               returns(float bin(24));
 
 hold_arg = arg;
 if hold_arg < float_m1
 | hold_arg > float_1 then
     signal error(3);
 i= 1;
 temp1 = float_0;
 if hold_arg >= float_m1
 & hold_arg < float_mv7071068 then do;
    temp1 = float_m1v57079625;
    hold_arg = sqrt(float_1 - hold_arg * hold_arg);
    end;
 else if hold_arg > float_v7071068
 & hold_arg <= float_1 then do;
     temp1 = float_1v57079625;

     i = -1;
     hold_arg = sqrt(float_1 - hold_arg * hold_arg);
     end;
 hold_arg = hold_arg * float_rt2;
 temp2 = hold_arg;
 hold_arg = ?fpex2(addr(fixed_2)->byte,hold_arg) * hold_arg - float_2;
 return(temp1 + i * temp2
           * ?cheby(hold_arg, addr(table_limit)->byte,parm_table));
 end asin;
