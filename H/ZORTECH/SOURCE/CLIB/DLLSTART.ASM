;_ dllstart.asm
; equivalent of winstart.asm except for DLL's instead of regular Windows apps
; Copyright (C) 1991 Walter Bright
; All Rights Reserved
; Written by: G. Eric Engstrom

includelib libw.lib

	DOSSEG		;have linker fix ordering of segments

	extrn	LIBMAIN		:far
        extrn	LOCALINIT	:far
	extrn	__cinit_f	:far


; A reference to __acrtused_dll is generated by the C/C++ compiler
; when a function LibMain is encountered and it was compiled with -W.
	public  __acrtused_dll
__acrtused_dll	equ	1234

	public	rsrvptrs

NULL	segment para public 'BEGDATA'
	;Windows seems to require this
	dw	0,0
rsrvptrs dw	5
	dw	0,0,0,0,0
NULL	ends

_DATA	segment para public 'DATA'
		db	0,1
__module_handle	dw	?		; Module handle
_DATA	ends
CDATA	segment word common 'DATA'
CDATA	ends
CONST	segment word public 'CONST'
CONST	ends
_BSS	segment word public 'BSS'
_BSS	ends
XIFB	segment word public 'DATA'
XIFB	ends
XIF	segment word public 'DATA'
XIF	ends
XIFE	segment word public 'DATA'
XIFE	ends
XIB	segment word public 'DATA'
XIB	ends
XI	segment word public 'DATA'
XI	ends
XIE	segment word public 'DATA'
XIE	ends
XOB	segment word public 'BSS'
XOB	ends
XO	segment word public 'BSS'
XO	ends
XOE	segment word public 'BSS'
XOE	ends
XPB	segment word public 'DATA'
XPB	ends
XP	segment word public 'DATA'
XP	ends
XPE	segment word public 'DATA'
XPE	ends
XCB	segment word public 'DATA'
XCB	ends
XC	segment word public 'DATA'
XC	ends
XCE	segment word public 'DATA'
XCE	ends
XCFB	segment word public 'DATA'
XCFB	ends
XCF	segment word public 'DATA'
XCF	ends
XCFE	segment word public 'DATA'
XCFE	ends
DBDATA	segment word common 'DATA'
DBDATA	ends

DGROUP	group	NULL,_DATA,CDATA,CONST,_BSS,XIFB,XIF,XIFE,XIB,XI,XIE,XOB,XO,XOE,XPB,XP,XPE,XCB,XC,XCE,XCFB,XCF,XCFE

_TEXT	segment para public 'CODE'
	assume	CS:_TEXT
        assume  DS:DGROUP

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
; On input:
;	SS:SP	Current user stack
;	DS	Selector for DLL's DGROUP, if any, otherwise, selector
;		for DGROUP of process attaching to DLL.
;		C and C++ DLLs always have a DGROUP.
; For Windows:
;	DI	instance handle
;	CX	heap size
;	ES:SI	command line
; Returns:
;	AX	0 error, !=0 success

	public	LibEntry

LibEntry proc	far
	cld			;no direction flag bugs
	;Arguments to _far _pascal LibMain(HANDLE,WORD,WORD,LPSTR)
	push	DI		;module instance handle
        push	DS		;data segment
        push	CX		;size of heap
        push	ES
        push	SI		;command line

	jcxz	L1			;if heap size is non-zero

	  ;LocalInit sets up the Windows heap
	  push	  DS
          xor	  AX,AX
          push	  AX
          push	  CX
          call	  LOCALINIT		;(DS:0,CX)
          or	  AX,AX
          jz	  L8			;error

L1:	mov	__module_handle,DI
        push    DI   	    	        ; Proposed by VSR
        call    __cinit_f               ; call static constructors
        pop     DI              	; Proposed by VSR
        call	LIBMAIN			; invoke the 'C' routine for initialization
        ret				; LibMain is responsible for stack clean up

L8:	add	SP,10			; clean up stack if LibMain not called
	ret
LibEntry endp

_TEXT	ends

	end	LibEntry
